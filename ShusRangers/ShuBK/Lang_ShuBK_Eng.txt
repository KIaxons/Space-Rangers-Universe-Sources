MicroModuls ^{
    ShuBK_Discount25 ^{
        Cost=75
        KindGraph=DiscountMM_25
        LockedAcryn=3
        Name=<"><">
        NonSearchable=1
        Priority=80
        Special=1
    }
    ShuBK_Discount50 ^{
        Cost=50
        KindGraph=DiscountMM_50
        LockedAcryn=3
        Name=<"><">
        NonSearchable=1
        Priority=50
        Special=1
    }
    ShuBK_Discount75 ^{
        Cost=25
        KindGraph=DiscountMM_75
        LockedAcryn=3
        Name=<"><">
        NonSearchable=1
        Priority=20
        Special=1
    }
	ShuBK_FullItemCost ^{
        LockedAcryn=3
        NonSearchable=1
        Special=1
    }
}
Script ^{
    Mod_ShuBK ^{
		GrandBKSale1=Today <clr>business center<clrEnd> #0# announced a major sale, with discounts ranging from <clr>25%<clrEnd> to <clr>75%<clrEnd> on most of its assortment from the equipment store. Hurry up and buy the newest parts for your ship at the lowest prices!
        GrandBKSale2=Recently <clr>business center<clrEnd> #0# announced big discounts on a significant portion of its range in the equipment store. The most budget-conscious rangers and entrepreneurs have already gone to the #1# system to purchase engines, fuel tanks, radars, scanners, and more at a bargain price.
        GrandBKSale3=All those who wish to purchase thoroughly are invited to the #0# system, in which <clr>business center<clrEnd> #1#, as part of its program to support low-income pilots, has reduced prices on a significant portion of the items in its equipment store.
        GrandBKSale4=Today <clrEnd>business center<clrEnd> #0#, located in the #1# system, has announced another grand sale of almost the entire range of equipment and weapons on sale at the station. The station's stores offer a wide selection with discounts from <clr>25%<clrEnd> to <clr>75%<clrEnd>!
        GrandBKSale5=<clrEnd>business center<clrEnd> #0# announces the start of a major equipment sale. All interested pilots are invited to the #1# system, where they can purchase parts at the lowest prices in the Galaxy!
    }
}
ShipInfo ^{
    AddInfo ^{
        CustomInfos ^{
            ShuBK_SellerBKMarkInfo ^{
                Description=Hidden infobox, marks a business center with a sale on it
                Icon=0
                Name=
				OnActCodeTypes=t_OnStep,t_OnDeath
				OnActStepTypes=11
                OnActCode ^{
					01=if(ScriptItemActionType(t_OnStep))
					02 ^{
						//Если время распродажи подошло к концу
						01=if(CurTurn() >= ShipCustomShipInfoData(0, CurInfo(), 1))
						02 ^{
							01=dword cur_ruin = ScriptItemActShip();
							02=int dis_25 = FindBonusByNameInCfg('ShuBK_Discount25');
							03=int dis_50 = FindBonusByNameInCfg('ShuBK_Discount50');
							04=int dis_75 = FindBonusByNameInCfg('ShuBK_Discount75');
							//Снимаем скидки со всех акционных предметов в магазине
							05=for(int i = 0; i < ShopItems(cur_ruin); i = i + 1)
							06 ^{
								01=dword item = ShopItems(cur_ruin, i);
								02=int cur_MM = EqModule(item);
								03=if(cur_MM != dis_75 && cur_MM != dis_50 && cur_MM != dis_25) continue;
								04=ModuleToEquipment(-1, item);
							}
							//Перебираем все акционные предметы игрока и обнуляем их маркеры изначальной цены
							//(чтобы он не мог починить их по дешёвке на следующей распродаже)
							08=int true_cost_bon = FindBonusByNameInCfg('ShuBK_FullItemCost');
							09=for(i = 0; i < ShipItems(Player()); i = i + 1)
							10 ^{
								01=item = ShipItems(Player(), i);
								02=int true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
								03=if(!true_cost) continue;
								04=ItemExtraSpecialsDeleteByType(item, true_cost_bon, true_cost);
							}
							11=for(i = 0; i < ShipArts(Player()); i = i + 1)
							12 ^{
								01=item = ShipArts(Player(), i);
								02=true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
								03=if(!true_cost) continue;
								04=ItemExtraSpecialsDeleteByType(item, true_cost_bon, true_cost);
							}
							13=for(i = 0; i < StorageItems(); i = i + 1)
							14 ^{
								01=if(StorageItemLocation(i) != cur_ruin) continue;
								02=item = StorageItems(i);
								//Пропускаем товары, а то будут крашить
								03=if(ItemType(item) <= t_Narcotics) continue;
								04=true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
								05=if(!true_cost) continue;
								06=ItemExtraSpecialsDeleteByType(item, true_cost_bon, true_cost);
							}
							15=ShipDeleteCustomShipInfo(cur_ruin, 'ShuBK_SellerBKMarkInfo');
						}
					}
					03=else
					04 ^{
						//Перебираем все акционные предметы игрока и обнуляем их маркеры изначальной цены
						//(чтобы он не мог починить их по дешёвке на следующей распродаже)
						01=true_cost_bon = FindBonusByNameInCfg('ShuBK_FullItemCost');
						02=for(i = 0; i < ShipItems(Player()); i = i + 1)
						03 ^{
							01=item = ShipItems(Player(), i);
							02=true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
							03=if(!true_cost) continue;
							04=ItemExtraSpecialsDeleteByType(item, true_cost_bon, true_cost);
						}
						04=for(i = 0; i < ShipArts(Player()); i = i + 1)
						05 ^{
							01=item = ShipArts(Player(), i);
							02=true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
							03=if(!true_cost) continue;
							04=ItemExtraSpecialsDeleteByType(item, true_cost_bon, true_cost);
						}
					}
				}
			}
            ShuBK_SaleInfo ^{
                Description=Hidden infobox, removes micromodules-discounts from items when you buy them
                Icon=0
                Name=
				OnActCodeTypes=t_OnStep,t_OnEnteringForm,t_OnReEnteringForm,t_OnLeavingForm
				OnActStepTypes=0
                OnActCode ^{
					01=dword cur_info = CurInfo();
					02=if(ScriptItemActionType(t_OnStep))
					03 ^{
						//Если игрок покидает БЦ
						01=if(ShipOrder(Player()) == 5)
						02 ^{
							//Перебираем все акционные предметы игрока и возвращаем их оригинальную стоимость
							01=dword shop = IdToShip(ShipCustomShipInfoData(0, cur_info, 1));
							02=int true_cost_bon = FindBonusByNameInCfg('ShuBK_FullItemCost');
							03=for(int i = 0; i < ShipItems(Player()); i = i + 1)
							04 ^{
								01=dword item = ShipItems(Player(), i);
								02=int true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
								03=if(!true_cost) continue;
								04=ItemCost(item, true_cost);
							}
							05=for(i = 0; i < ShipArts(Player()); i = i + 1)
							06 ^{
								01=item = ShipArts(Player(), i);
								02=true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
								03=if(!true_cost) continue;
								04=ItemCost(item, true_cost);
							}
							07=for(i = 0; i < StorageItems(); i = i + 1)
							08 ^{
								01=if(StorageItemLocation(i) != shop) continue;
								02=item = StorageItems(i);
								//Пропускаем товары, а то будут крашить
								03=if(ItemType(item) <= t_Narcotics) continue;
								04=true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
								05=if(!true_cost) continue;
								06=ItemCost(item, true_cost);
							}
							09=ShipDeleteCustomShipInfo(Player(), 'ShuBK_SaleInfo');
						}
						03=else if(!ShipCustomShipInfoData(0, cur_info, 2))
						04 ^{
							01=int dis_25 = FindBonusByNameInCfg('ShuBK_Discount25');
							02=int dis_50 = FindBonusByNameInCfg('ShuBK_Discount50');
							03=int dis_75 = FindBonusByNameInCfg('ShuBK_Discount75');
							//Перебираем галасобытия, ищем свежекупленные предметы
							04=for(i = GalaxyEvents() - 1; i >= 0; i = i - 1)
							05 ^{
								01=if(i <= ShipCustomShipInfoData(0, cur_info, 2)) break;
								02=if(GalaxyEventDate(i) < CurTurn()) break;
								03=if(GalaxyEventType(i) == 'PlayerBuysEquipment')
								04 ^{
									01=if(GalaxyEventData(i, 5) == ShipCustomShipInfoData(0, cur_info, 1))
									02 ^{
										01=item = IdToItem(GalaxyEventData(i, 3));
										02=int cur_MM = EqModule(item);
										//Снимаем скидку, если нашли таковую на недавно купленном предмете
										03=if(cur_MM != dis_75 && cur_MM != dis_50 && cur_MM != dis_25) continue;
										04=int dis_cost = ItemCost(item);
										05=ModuleToEquipment(-1, item);
										06=ItemCost(item, dis_cost);
									}
								}
							}
							//Запоминаем просмотренные номера событий, чтобы не прогонять по ним предметы второй раз
							06=ShipCustomShipInfoData(0, cur_info, 2, GalaxyEvents() - 1);
						}
						05=else ShipCustomShipInfoData(0, cur_info, 2, 0);
					}
					04=else if(ScriptItemActionType(t_OnEnteringForm) || ScriptItemActionType(t_OnReEnteringForm))
					05 ^{
						01=if(CurrentForm() == 'Ship')
						02 ^{
							01=dis_25 = FindBonusByNameInCfg('ShuBK_Discount25');
							02=dis_50 = FindBonusByNameInCfg('ShuBK_Discount50');
							03=dis_75 = FindBonusByNameInCfg('ShuBK_Discount75');
							//Перебираем галасобытия, ищем свежекупленные предметы
							04=for(i = GalaxyEvents() - 1; i >= 0; i = i - 1)
							05 ^{
								01=if(i <= ShipCustomShipInfoData(0, cur_info, 2)) break;
								02=if(GalaxyEventDate(i) < CurTurn()) break;
								03=if(GalaxyEventType(i) == 'PlayerBuysEquipment')
								04 ^{
									01=if(GalaxyEventData(i, 5) == ShipCustomShipInfoData(0, cur_info, 1))
									02 ^{
										01=item = IdToItem(GalaxyEventData(i, 3));
										02=cur_MM = EqModule(item);
										//Снимаем скидку, если нашли таковую на недавно купленном предмете
										03=if(cur_MM != dis_75 && cur_MM != dis_50 && cur_MM != dis_25) continue;
										04=dis_cost = ItemCost(item);
										05=ModuleToEquipment(-1, item);
										06=ItemCost(item, dis_cost);
									}
								}
							}
							//Запоминаем просмотренные номера событий, чтобы не прогонять по ним предметы второй раз
							06=ShipCustomShipInfoData(0, cur_info, 2, GalaxyEvents() - 1);
							//Перебираем предметы в магазине и навешиваем им скидки обратно, если игрок решил продать только что купленный на распродаже предмет
							07=true_cost_bon = FindBonusByNameInCfg('ShuBK_FullItemCost');
							08=shop = IdToShip(ShipCustomShipInfoData(0, cur_info, 1));
							09=for(i = 0; i < ShopItems(shop); i = i + 1)
							10 ^{
								01=item = ShopItems(shop, i);
								02=cur_MM = EqModule(item);
								03=if(cur_MM == dis_75 || cur_MM == dis_50 || cur_MM == dis_25) continue;
								04=true_cost = ItemExtraSpecialsCountByType(item, true_cost_bon);
								05=if(!true_cost) continue;
								06=ItemCost(item, true_cost);
								07=ItemExtraSpecialsDeleteByType(item, true_cost_bon, true_cost);
								//Если распродажа в БЦ всё ещё продолжается
								08=if(ShipFindCustomShipInfoByType(shop, 'ShuBK_SellerBKMarkInfo') == -1) continue;
								09=int level = ItemLevel(item);
								10=if(level < GalaxyTechLevel() - 1)
								11 ^{
									01=if(EqModule(item) == -1) ModuleToEquipment(dis_75, item);
								}
								12=else if(level < GalaxyTechLevel())
								13 ^{
									01=if(EqModule(item) == -1) ModuleToEquipment(dis_50, item);
								}
								14=else if(level >= GalaxyTechLevel())
								15 ^{
									01=if(EqModule(item) == -1) ModuleToEquipment(dis_25, item);
								}
							}
						}
					}
					06=else if(ScriptItemActionType(t_OnLeavingForm))
					07 ^{
						01=if(CurrentForm() == 'EquipmentShop')
						02 ^{
							01=dis_25 = FindBonusByNameInCfg('ShuBK_Discount25');
							02=dis_50 = FindBonusByNameInCfg('ShuBK_Discount50');
							03=dis_75 = FindBonusByNameInCfg('ShuBK_Discount75');
							//Перебираем галасобытия, ищем свежекупленные предметы
							04=for(i = GalaxyEvents() - 1; i >= 0; i = i - 1)
							05 ^{
								01=if(i <= ShipCustomShipInfoData(0, cur_info, 2)) break;
								02=if(GalaxyEventDate(i) < CurTurn()) break;
								03=if(GalaxyEventType(i) == 'PlayerBuysEquipment')
								04 ^{
									01=if(GalaxyEventData(i, 5) == ShipCustomShipInfoData(0, cur_info, 1))
									02 ^{
										01=item = IdToItem(GalaxyEventData(i, 3));
										02=cur_MM = EqModule(item);
										//Снимаем скидку, если нашли таковую на недавно купленном предмете
										03=if(cur_MM != dis_75 && cur_MM != dis_50 && cur_MM != dis_25) continue;
										04=dis_cost = ItemCost(item);
										05=ModuleToEquipment(-1, item);
										06=ItemCost(item, dis_cost);
									}
								}
							}
							//Запоминаем просмотренные номера событий, чтобы не прогонять по ним предметы второй раз
							06=ShipCustomShipInfoData(0, cur_info, 2, GalaxyEvents() - 1);
						}
					}
                }
            }
        }
    }
}